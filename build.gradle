plugins {
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'com.adarshr.test-logger' version '3.0.0'
    id 'org.springframework.boot' version '2.5.4' apply false
}

ext.versions = [
        assertj           : '2.8.0',
        resilience4j      : '1.7.0',
        retrofit          : '2.9.0',
        springBoot        : '2.5.7',
        springCloud       : '2020.0.3',
        springCloudAws    : '2.3.2',
        springSession     : '2020.0.7',
        swaggerAnnotations: '2.1.11',
        log4j : '2.16.0',
]

allprojects {
    apply plugin: 'io.spring.dependency-management'
    group 'com.personal.finance'
    version '1.0-SNAPSHOT'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${versions.springBoot}")
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:${versions.springCloud}")
            mavenBom("io.awspring.cloud:spring-cloud-aws-dependencies:${versions.springCloudAws}")
            mavenBom("io.github.resilience4j:resilience4j-bom:${versions.resilience4j}")
            mavenBom("org.springframework.session:spring-session-bom:${versions.springSession}")
        }

        dependencies {
            // https://mvnrepository.com/artifact/org.json/json
            dependency group: 'org.json', name: 'json', version: '20211205'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'
    apply plugin: 'com.adarshr.test-logger'

    dependencies {
        implementation group: 'org.codehaus.groovy', name: 'groovy'
        implementation group: 'org.codehaus.groovy', name: 'groovy-json'

        testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.1-M2-groovy-3.0'
        testImplementation group: 'org.spockframework', name: 'spock-spring', version: '2.1-M2-groovy-3.0'
        testImplementation group: 'org.assertj', name: 'assertj-core'
        testImplementation group: 'junit', name: 'junit'
        implementation group: 'org.codehaus.groovy', name: 'groovy'
        implementation group: 'org.codehaus.groovy', name: 'groovy-json'

        testImplementation group: 'org.spockframework', name: 'spock-core'
        testImplementation group: 'org.spockframework', name: 'spock-spring'
    }

    test {
        useJUnitPlatform()
    }

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(11)
        }
        withSourcesJar()
    }

    jacocoTestReport {
        reports {
            html.enabled(true)
            xml.enabled(true)
            csv.enabled(false)
            html.destination file("${buildDir}/${project.name}")
        }
    }
    test.finalizedBy jacocoTestReport
}